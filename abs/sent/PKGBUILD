# Maintainer: David Phillips <dbphillipsnz@gmail.com>

_pkgname=sent
pkgname=${_pkgname}-git
pkgver=1.3.g72d33d4
pkgrel=1
pkgdesc="Simple plain-text presentation tool"
arch=('i686' 'x86_64')
url="http://tools.suckless.org/${_pkgname}"
license=('MIT')
depends=('fontconfig' 'libxft')
optdepends=('farbfeld: PNG and JPEG support')
makedepends=('git')
conflicts=('sent')
source=("${pkgname}::git://git.suckless.org/${_pkgname}"
        "config.h")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
	cd "${pkgname}"
	git describe | sed 's/-/./g'
}

prepare() {
	cd "${srcdir}/${pkgname}"
	ln -sf "../../config.h" .
}

build() {
	make -C "${srcdir}/${pkgname}"
}

package() {
	cd "${srcdir}/${pkgname}"
	make PREFIX="/usr" \
		 DESTDIR="${pkgdir}" \
		 install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
